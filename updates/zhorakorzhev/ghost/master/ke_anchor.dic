OnTranslate
{
	_talk = reference0
	
	//---Anchors---
	_talk = REPLACE(_talk, "Example Anchor", "\_a[exampleanchor]Example Anchor\_a")
	_talk = REPLACE(_talk, "Example word swap", "You don't have to make anchors into links, you can put images and stuff too")
	
	//code stuff
	_talk = REPLACE(_talk, "\к", "\e"); _talk = REPLACE(_talk, "\ж", "\w"); _talk = REPLACE(_talk, "\н", "\n")
	_talk = REPLACE(_talk, "\п", "\s"); _talk = REPLACE(_talk, "\х", "\p"); _talk = REPLACE(_talk, "(№", "[")
	_talk = REPLACE(_talk, "№)", "]")
	//surface is already taken as П(оверхность) so \p (which i assume means person...? gets Ч(еловек) or, for right now, Х(арактер). it's not perfect but it'll do.
	//the balloon tag would also use п so something needs to be done about that...
	
	//---Important other stuff, written by Ponapalt---
	if reference1 == "" && reference2 == ""
	{ //send input box : no event (ref2) , no special flag (ref1)
		EVAL('"' + REPLACE(_talk,'"','" + CHR(0x22) + "') + '"')
	}
    else
	{ //event result from ghost
		TOSTR(_talk)
	}
}

OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0 //checking to see if the link is a web address
	{
		"\j[%(reference0)]\e"
	}
	else
	{
		case reference0
		{
			when "exampleanchor"
			{
				"\0\s[0]You clicked the example!\e"
			}
		}
	}
}



//As the name implies, this checks if the balloon is currently open or not. This was written by Levidre!
//To use it I would do this, so you don't have to copy this whole chunk of code:
//_b = BalloonCheck
//if _b == 0 //_b will be 0 if the balloon is not open, or 1 if it is open
BalloonCheck
{
	if ASEARCH("Status",var.req.key) != -1
    {
        _currentstatus = var.req.value[ASEARCH("Status",var.req.key)]
        if "balloon" _in_ _currentstatus
        {
            1
        }
        else
        {
            0
        }
    }
}

//Setting variables in random dialogue
//In the YAYA tips, I go into detail about setting single variables in random dialogue. This function will allow you to set more than one, or anything else you might want to do. Call it with \![raise,OnDialogueVars,yourValue], replacing yourValue with whatever you want reference0 to be.
OnDialogueVars
{
	case reference0
	{
		when "example"
		{
			_example += 1
		}
	}
}