//I put anything related to who the user is in here. Name, pronouns, birthday.

OnChangeName
{
	"\0\s[0]\![set,balloontimeout,-1]Ах... как тебя зовут?\![open,inputbox,OnNameTeach,-1]"
}

OnNameTeach
{
	_tempname = reference0 //this stores the original name in _tempname
	reference0 = TOLOWER(reference0) //changes it to lowercase
	reference0 = REPLACE(reference0," ","") //removes spaces
	reference0 = REPLACE(reference0,".","") //removes periods
	reference0 = REPLACE(reference0,"-","") //removes dashes
	
	if _tempname == имя && booting != 1 //If the user gives the exact same name as before. The booting check has to be there in case they put in 'user'
	{
		"\0\s[0]...То же самая?"
	}
	/*elseif "template" _in_ reference0 || _tempname == "X." || _tempname == "x." //Checking with _tempname because 'X.' has a period, which would be removed. I don't want to stop someone putting in just X
	{
		"\0\s[0]That's my name!\w8\w8 I can't call you that!\w8\w8 Try again.\![open,inputbox,OnNameTeach,-1]\e"
	}*/
	elseif reference0 == "" //If the user doesn't put in anything
	{
		имя = "Пользователь"
		
		"\0\s[0]Просто %(имя).\w6 Расскажи мне если этого не хочешь!!"
	}
	else //if it's none of these above names, we can use it. You can add names that just have comments and can still be used, just don't add a \e at the end.
	{
		имя = _tempname
		"\0\s[0]%(имя)? Мне нравится! Очень хорошая имя."
	}
	--
	if booting == 0 //If the user was changing their name through the config menu
	{
		"\n\n\![*]\q[Назад,OnConfigMenu] \![*]\q[Всё,cancelnodialogue]\e"
	}
	else //This is continuing from FirstBoot
	{
		"\w6Доро познакомниться!\w4И... местоимение?\n\n"
		--
		"\![*]\q[Он/Его,OnPronouns,masc]\n/
		\![*]\q[Она/Ей,OnPronouns,fem]\n/	
		\![*]\q[Они/Их,OnPronouns,undef]\e"
	}
}

OnChangePronoun //Envelope names are the same as the GT template, since I figured most people will be used to them.
{
	"\0\s[0]А твои местоимение?"
	--
	"\n\n\_q/
	\![*]\q[Он/Его,OnPronouns,masc]\n/
	\![*]\q[Она/Её,OnPronouns,fem]\n/	
	\![*]\q[Они/Их,OnPronouns,undef]\e"
}


OnPronouns
{
	if reference0 == "masc"
	{
		он = "он" //именительный падеж
		его = "его" //родительный падеж
		него = "него"
		ему = "ему" //дательный падеж
		//его = "его" //винительный падеж
		им = "им" //творительный падеж
		ним = "ним"
		нём = "нём"//предложный падеж
		л = "л"
		лся = "лся"
	}
	//nominative, genetive, dative, accusative, instrumental and prepositional
	elseif reference0 == "fem"
	{
		он = "она" //именительный падеж
		его = "её" //родительный падеж
		него = "неё"
		ему = "ей" //дательный падеж
		//его = "его" //винительный падеж
		им = "ей" //творительный падеж
		ним = "ней"
		нём = "ней"//предложный падеж
		л = "ла"
		лся = "лась"
	}
	else
	{
		он = "они" //именительный падеж
		его = "их" //родительный падеж
		него = "них"
		ему = "им" //дательный падеж
		//его = "его" //винительный падеж
		им = "ими" //творительный падеж
		ним = "ними"
		нём = "них"//предложный падеж
		л = "ли"
		лся = "лись"
		
	}
	--
	if booting == 1 //This is the end of the firstboot dialogue
	{
		booting = 0
		"Хорошо, я не забуду. Думаю что мы скоро будем друзья!\w6\![leave,passivemode]\e"
	}
	else //This is if the user is changing their pronouns through the menu
	{
		"\0\s[0]Теперь всё в порядке."
		--
		"\n\n\![*]\q[Назад,OnConfigMenu] \![*]\q[Всё,cancelnodialogue]\e"
	}
}
//Pronoun examples for my own sake:
//"%(heshe) left that there."
//"%(hisher) work is really cool."
//"I'll ask %(himher) later."
//"%(hesshes) really good at it."

OnChangeBirthday //Pulled a lot of this from the GT template, but I also modified it a lot
{
	"\0\s[0]Ты роди%(лся) в какого месяца?\w8\w8 Можно написать имя или номер, это не важно."
	--
	"\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\e"
}

//birthday[0] - word birthday
//birthday[1] - number birthday
//birthday[2] - word month
//birthday[3] - word day
//birthday[4] - number month
//birthday[5] - number day
//Use birthday[0] to display the user's birthday, and birthday[1] to do checks with the numbers

OnUserBornMonth
{
	_tempmonth = reference0 //this stores what the user put in as a variable called _tempmonth
	_tempmonth = TOLOWER(_tempmonth) //changes it to lowercase
	
	if _tempmonth == "январь" || _tempmonth == "1" {birthday[4] = 1; birthday[2] = "Январья"}
	elseif _tempmonth == "февраль" || _tempmonth == "2" {birthday[4] = 2; birthday[2] = "Февраля"}
	elseif _tempmonth == "март" || _tempmonth == "3" {birthday[4] = 3; birthday[2] = "Марта"}
	elseif _tempmonth == "апрель" || _tempmonth == "4" {birthday[4] = 4; birthday[2] = "Апреля"}
	elseif _tempmonth == "май" || _tempmonth == "5" {birthday[4] = 5; birthday[2] = "Май"}
	elseif _tempmonth == "июнь" || _tempmonth == "6" {birthday[4] = 6; birthday[2] = "Июня"}
	elseif _tempmonth == "июль" || _tempmonth == "7" {birthday[4] = 7; birthday[2] = "Июля"}
	elseif _tempmonth == "августь" || _tempmonth == "8" {birthday[4] = 8; birthday[2] = "Августь"}
	elseif _tempmonth == "сентябрь" || _tempmonth == "9" {birthday[4] = 9; birthday[2] = "Сентября"}
	elseif _tempmonth == "октябрь" || _tempmonth == "10" {birthday[4] = 10; birthday[2] = "Октября"}
	elseif _tempmonth == "ноябрь" || _tempmonth == "11" {birthday[4] = 11; birthday[2] = "Ноября"}
	elseif _tempmonth == "декабрь" || _tempmonth == "12" {birthday[4] = 12; birthday[2] = "Декабря"}
	else //if the month isn't a valid month, like 32 or something.
	{
		"\0\s[0]А %(_tempmonth), это что за месяць?\w4 ...Имя или номер."
		--
		"\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\e"
	}
	--
	//If it's a valid month
	"%(birthday[2])\w6 Ок, ок, ок,\w4 а день? Напиши просто номер, пожалуйста. (Например: 19)"
	--
	"\![open,inputbox,OnUserBornDay,-1]\![set,balloontimeout,-1]\e"
}

OnUserBornDay
{
	_tempday = reference0 //storing the input in _tempday
	_tempday = TOLOWER(_tempday) //changes it to lowercase
	_tempday = REPLACE(_tempday,"st","") //if the user types something like 2nd, 3rd, etc, this will make it just the number. It'll just add the suffix back later on the one that actually shows.
	_tempday = REPLACE(_tempday,"nd","")
	_tempday = REPLACE(_tempday,"rd","")
	_tempday = REPLACE(_tempday,"th","")
	_tempday = TOINT(_tempday) //Converts it to an integer
	_month = birthday[4] //Just for readability
	
	if ((_month == 1 || _month == 3 || _month == 5 || _month == 7 || _month == 8 || _month == 10 || _month == 12) && _tempday >= 1 && _tempday <= 31) || (_month == 2 && _tempday >= 1 && _tempday <= 29) || ((_month == 4 || _month == 6 || _month == 9 || _month == 11) && _tempday >= 1 && _tempday <= 30) //This ugly function checks if the day falls into a valid range. If it does, we set some variables and continue down.
	{
		birthday[5] = _tempday
		birthday[3] = Printday
		birthday[1] = "%(birthday[5])/%(birthday[4])" //If you want to write dates as DD/MM, feel free to swap this around
		birthday[0] = "%(birthday[3]) %(birthday[2])"
	}
	else //If it isn't a valid day, we open the input box again and have a \e so the dialogue below doesn't play.
	{
		"\0\s[0]...Ну я не думаю что это прав.\w6 Снова?"
		--
		"\![open,inputbox,OnUserBornDay,-1]\e"
	}
	-- 
	//All of these only happen if it's a valid birthday
	if birthday[1] == "%(day)/%(month)" //if today is the user's birthday. Don't forget to switch this if you change the format to DD/MM
	{
		"\0\s[0]Ой, с днём рождения\w4 %(username)! Почему мне не рассказа%(л) раньше?"
	}
	/*elseif birthday[1] == "12/25" //If it's on christmas. Put any date you'd like here. If you change the format to DD/MM, make sure you change any dates like this that might be in bootend.
	{
		"\0\s[0]Oh,\w4 you were born on Christmas?\w8\w8 Cool!"
	}*/
	else //if the birthday is not listed above.
	{
		"\0\s[0]Отлично! %(birthday[0])... Я просто не могу ждать!!"
	}
	--
	"\n\n\![*]\q[Назад,OnConfigMenu] \![*]\q[Всё,cancelnodialogue]\e"
}

Printday //Sets the appropriate suffix
{
	/*if birthday[5] == 1 || birthday[5] == 21 || birthday[5] == 31
	{
		"%(birthday[5])st"
	}
	elseif birthday[5] == 2 || birthday[5] == 22
	{
		"%(birthday[5])nd"
	}
	elseif birthday[5] == 3 || birthday[5] == 23
	{
		"%(birthday[5])rd"
	}
	else
	{
		"%(birthday[5])th"
	}*/
}	